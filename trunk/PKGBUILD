# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Markus Richter <mqus at disroot dot org>

pkgname=bitwarden_rs-web
_pkgver=2.19.0b
pkgver=2.19.0.b
_upstreamver=2.19.0
_patchver=2.19.0
pkgrel=1
pkgdesc='Bitwarden web vault with the patches to make it work with bitwarden_rs'
arch=('any')
url=https://github.com/dani-garcia/bw_web_builds
license=('GPL3')
depends=('bitwarden_rs')
makedepends=('git' 'nodejs-lts-fermium' 'npm' 'python2')
provides=('bitwarden_rs-vault')
replaces=('bitwarden_rs-vault')
install=$pkgname.install
source=("bitwarden-web::git+https://github.com/bitwarden/web.git#tag=v$_upstreamver"
        'git+https://github.com/bitwarden/jslib.git'
        "$pkgname-v$_patchver.patch::https://raw.githubusercontent.com/dani-garcia/bw_web_builds/v$_pkgver/patches/v$_patchver.patch")
b2sums=('SKIP'
        'SKIP'
        '3ab0678561cea99b652a87a4ad1780fa21d376e0dc1472eff5e3ab6c0f9942c836f1e0d8e05daf43b82ff0ea0d2fe295ffc9fb1207e1cb17971dc2ec0c117582')

prepare() {
  cd bitwarden-web
  git submodule init
  git config submodule.jslib.url ../jslib
  git submodule update
  patch --forward --strip=1 --input=../$pkgname-v$_patchver.patch
  npm ci
}

build() {
  cd bitwarden-web
  npm run dist
}

package() {
  cd bitwarden-web
  install -d "$pkgdir"/usr/share/$pkgname
  cp -R build/* "$pkgdir"/usr/share/$pkgname
}

# vim:set ts=2 sw=2 et:
